#
#                                Copyright (C) 2017 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#

include on freebsd ~/toolsets/freebsd/freebsd-module.hsl
include on linux ~/toolsets/linux/linux-module.hsl
include mkclean.hsl
include krun.hsl

local var includes type list;
local var cflags type list;
local var libraries type list;
local var ldflags type list;

local var LKMTOOL type string;

project kryptos-test-mod : toolset $LKMTOOL : "main.c", $includes, $cflags, $libraries, $ldflags, "kryptos_test";

kryptos-test-mod.preloading() {
    if (hefesto.sys.os_name() == "freebsd") {
        $LKMTOOL = "freebsd-kld";
    } else if (hefesto.sys.os_name() == "linux") {
        $LKMTOOL = "linux-lkm";
    }
}

kryptos-test-mod.prologue() {
    $includes.add_item("../../");
    $includes.add_item(hefesto.sys.pwd());

    var option type list;
    $option = hefesto.sys.get_option("clean");

    if ($option.count() > 0) {
        mkclean($includes);
        hefesto.sys.echo("*** All clean!\n");
        hefesto.project.abort(0);
    }

    # INFO(Rafael): Only define KRYPTOS_KERNEL_MODE will guide the compiler across the build of the kryptos kernel mode version.
    #               Notice that no .ar file is generated. When adding the path "../.." to the includes list the object files from
    #               kryptos are automatically linked together.
    $cflags.add_item("-DKRYPTOS_KERNEL_MODE=1");
}

kryptos-test-mod.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        hefesto.sys.echo("*** Test module successfully compiled.\n");
        var exit_code type int;
        $exit_code = krun("kryptos_test.ko");
        if ($exit_code != 0) {
            hefesto.project.abort($exit_code);
        }
    } else {
        hefesto.sys.echo("~~~ Error during test module compilation.\n");
    }
}
